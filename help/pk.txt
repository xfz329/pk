PK.SE.PKJ.SEJ.from.xy.data
  Copyright 1996, Warren D. Smith
=COPY()
=新建(1)
=SET.NAME("Wksht",GET.DOCUMENT(1))
=ACTIVATE(Wksht)
=COLUMN.WIDTH(4,!$A$1:!$GZ$1)
=COLUMN.WIDTH(5,!$F$1:!$G$1)
=COLUMN.WIDTH(6,!$H$1)
=SELECT(!$A$2)
=PASTE()

=SET.NAME("xy",SELECTION())
=FORMULA("x",OFFSET(xy,-1,0))
=FORMULA("y",OFFSET(xy,-1,1))
=SET.NAME("Ncases",ROWS(xy))
=SET.NAME("Colxy",COLUMNS(xy))
=IF(Ncases<2)
=  BEEP()
=  FORMULA("Not enough cases (rows) selected; need at least two.",OFFSET(xy,0,5,1,1))
=  HALT()
=ELSE.IF(Colxy<>2)
=  BEEP()
=  FORMULA("Must select exactly two columns; x-values in left, y-values in right.",OFFSET(xy,0,5,1,1))
=  HALT()
=END.IF()

=SELECT(OFFSET(xy,0,0,Ncases,Colxy+1))
=FORMULA("k",OFFSET(xy,-1,Colxy))
=FOR("k",1,Ncases,1)
=  FORMULA(k,INDEX(SELECTION(),k,3))
=NEXT()

=SORT(1,INDEX(SELECTION(),1,2),1)
=SELECT(OFFSET(xy,0,0,Ncases,Colxy+2))
=FORMULA("Ry",OFFSET(xy,-1,Colxy+1))
=SET.NAME("y",INDEX(SELECTION(),1,2))
=SET.NAME("Rows",1)
=SET.NAME("JackOK",1)
=SET.NAME("Ncasrow",0)
=FOR("k",1,Ncases,1)
=  IF(INDEX(SELECTION(),k,2)=y)
=    SET.NAME("Ncasrow",Ncasrow+1)
=  ELSE.IF(INDEX(SELECTION(),k,2)<>y)
=    SET.NAME("Rows",Rows+1)
=    SET.NAME("y",INDEX(SELECTION(),k,2))
=    IF(Ncasrow<2)
=      SET.NAME("JackOK",0)
=    END.IF()
=    SET.NAME("Ncasrow",1)
=  END.IF()
=  FORMULA(Rows,INDEX(SELECTION(),k,4))
=NEXT()
=IF(Ncasrow<2)
=  SET.NAME("JackOK",0)
=END.IF()
=IF(Rows>2)
=  SET.NAME("JackOK",1)
=END.IF()

=IF(Rows<2)
=  BEEP()
=  FORMULA("Only one distinct y-value; need at least two.",OFFSET(xy,0,5,1,1))
=  HALT()
=END.IF()

=SORT(1,INDEX(SELECTION(),1,1),1,INDEX(SELECTION(),1,2),1)
=SELECT(OFFSET(xy,0,0,Ncases,Colxy+3))
=FORMULA("Cx",OFFSET(xy,-1,Colxy+2,1,1))
=SET.NAME("x",INDEX(SELECTION(),1,1))
=SET.NAME("Cols",1)
=FOR("k",1,Ncases,1)
=  IF(INDEX(SELECTION(),k,1)<>x)
=    SET.NAME("Cols",Cols+1)
=    SET.NAME("x",INDEX(SELECTION(),k,1))
=  END.IF()
=  FORMULA(Cols,INDEX(SELECTION(),k,5))
=NEXT()
=SORT(1,INDEX(SELECTION(),1,3),1)

=SET.NAME("RyCx",OFFSET(xy,0,3,Ncases,2))
=SET.NAME("aij",OFFSET(RyCx,7,6,Rows,Cols))
=FORMULA("aij",OFFSET(aij,-1,0,1,1))
=FORMULA.FILL(0,aij)

=FOR("k",1,Ncases,1)
=  SET.NAME("Ry",INDEX(RyCx,k,1))
=  SET.NAME("Cx",INDEX(RyCx,k,2))
=  SET.NAME("a",INDEX(aij,Ry,Cx))
=  SET.NAME("a",a+1)
=  FORMULA(a,INDEX(aij,Ry,Cx))
=NEXT()

=SELECT(aij)
=SET.NAME("Offset",1)
=SET.NAME("sij",OFFSET(aij,Rows+Offset,0,Rows,Cols+1))
=FORMULA("sij",OFFSET(sij,-1,0,1,1))
=SET.NAME("Cij",OFFSET(sij,Rows+Offset,0,Rows,Cols))
=FORMULA("Cij",OFFSET(Cij,-1,0,1,1))
=SET.NAME("Dij",OFFSET(Cij,Rows+Offset,0,Rows,Cols))
=FORMULA("Dij",OFFSET(Dij,-1,0,1,1))
=SET.NAME("Tij",OFFSET(Dij,Rows+Offset,0,Rows,Cols))
=FORMULA("Tij",OFFSET(Tij,-1,0,1,1))

=FOR("ai",1,Rows,1)
=  FORMULA(INDEX(aij,ai,1),INDEX(sij,ai,1))
=  FOR("aj",2,Cols,1)
=    FORMULA(INDEX(sij,ai,aj-1)+INDEX(aij,ai,aj),INDEX(sij,ai,aj))
=  NEXT()
=NEXT()

=FOR("CDTi",1,Rows,1)
=  FOR("CDTj",1,Cols,1)
=    IF(INDEX(aij,CDTi,CDTj)<>0,GOTO($A$115),GOTO($A$140))
=      FOR("ai",1,Rows,1)
=        IF(ai<CDTi)
=          IF(CDTj>1)
=            FORMULA(INDEX(Cij,CDTi,CDTj)+INDEX(sij,ai,CDTj-1),INDEX(Cij,CDTi,CDTj))
=          END.IF()
=          IF(CDTj<Cols)
=            FORMULA(INDEX(Dij,CDTi,CDTj)+(INDEX(sij,ai,Cols)-INDEX(sij,ai,CDTj)),INDEX(Dij,CDTi,CDTj))
=          END.IF()
=          FORMULA(INDEX(Tij,CDTi,CDTj)+INDEX(aij,ai,CDTj),INDEX(Tij,CDTi,CDTj))
=        ELSE.IF(ai>CDTi)
=          IF(CDTj<Cols)
=            FORMULA(INDEX(Cij,CDTi,CDTj)+(INDEX(sij,ai,Cols)-INDEX(sij,ai,CDTj)),INDEX(Cij,CDTi,CDTj))
=          END.IF()
=          IF(CDTj>1)
=            FORMULA(INDEX(Dij,CDTi,CDTj)+INDEX(sij,ai,CDTj-1),INDEX(Dij,CDTi,CDTj))
=          END.IF()
=          FORMULA(INDEX(Tij,CDTi,CDTj)+INDEX(aij,ai,CDTj),INDEX(Tij,CDTi,CDTj))
=        END.IF()
=      NEXT()
=      FORMULA(OFFSET(Cij,CDTi-1,Cols+1,1,1)+INDEX(aij,CDTi,CDTj)*INDEX(Cij,CDTi,CDTj),OFFSET(Cij,CDTi-1,Cols+1,1,1))
=      FORMULA(OFFSET(Dij,CDTi-1,Cols+1,1,1)+INDEX(aij,CDTi,CDTj)*INDEX(Dij,CDTi,CDTj),OFFSET(Dij,CDTi-1,Cols+1,1,1))
=      FORMULA(OFFSET(Tij,CDTi-1,Cols+1,1,1)+INDEX(aij,CDTi,CDTj)*INDEX(Tij,CDTi,CDTj),OFFSET(Tij,CDTi-1,Cols+1,1,1))
=      FORMULA(OFFSET(Cij,1,Cols+2,1,1)+INDEX(aij,CDTi,CDTj)*INDEX(Cij,CDTi,CDTj)*INDEX(Cij,CDTi,CDTj),OFFSET(Cij,1,Cols+2,1,1))
=      FORMULA(OFFSET(Dij,1,Cols+2,1,1)+INDEX(aij,CDTi,CDTj)*INDEX(Dij,CDTi,CDTj)*INDEX(Dij,CDTi,CDTj),OFFSET(Dij,1,Cols+2,1,1))
=      FORMULA(OFFSET(Tij,1,Cols+2,1,1)+INDEX(aij,CDTi,CDTj)*INDEX(Cij,CDTi,CDTj)*INDEX(Dij,CDTi,CDTj),OFFSET(Tij,1,Cols+2,1,1))
=  NEXT()
=  FORMULA(OFFSET(sij,0,Cols+1,1,1)+INDEX(sij,CDTi,Cols),OFFSET(sij,0,Cols+1,1,1))
=  FORMULA(OFFSET(Cij,0,Cols+2,1,1)+OFFSET(Cij,CDTi-1,Cols+1,1,1),OFFSET(Cij,0,Cols+2,1,1))
=  FORMULA(OFFSET(Dij,0,Cols+2,1,1)+OFFSET(Dij,CDTi-1,Cols+1,1,1),OFFSET(Dij,0,Cols+2,1,1))
=  FORMULA(OFFSET(Tij,0,Cols+2,1,1)+OFFSET(Tij,CDTi-1,Cols+1,1,1),OFFSET(Tij,0,Cols+2,1,1))
=NEXT()

=SET.NAME("n",DEREF(OFFSET(sij,0,Cols+1,1,1)))
=SET.NAME("Qc",DEREF(OFFSET(Cij,0,Cols+2,1,1)))
=SET.NAME("Qd",DEREF(OFFSET(Dij,0,Cols+2,1,1)))
=SET.NAME("Qtx",DEREF(OFFSET(Tij,0,Cols+2,1,1)))
=SET.NAME("Qcdt",Qc+Qd+Qtx)
=SET.NAME("dyx",(Qc-Qd)/Qcdt)
=SET.NAME("PK",(dyx+1)/2)

=SET.NAME("Qcc",DEREF(OFFSET(Cij,1,Cols+2,1,1)))
=SET.NAME("Qdd",DEREF(OFFSET(Dij,1,Cols+2,1,1)))
=SET.NAME("Qcd",DEREF(OFFSET(Tij,1,Cols+2,1,1)))
=SET.NAME("Term1",Qcc-2*Qcd+Qdd)
=SET.NAME("Term2",0)
=SET.NAME("Term3",0)
=FOR("CDTi",1,Rows,1)
=  SET.NAME("ni",DEREF(INDEX(sij,CDTi,Cols)))
=  SET.NAME("Qci",DEREF(OFFSET(Cij,CDTi-1,Cols+1,1,1)))
=  SET.NAME("Qdi",DEREF(OFFSET(Dij,CDTi-1,Cols+1,1,1)))
=  SET.NAME("Term2",Term2+(n-ni)*(Qci-Qdi))
=  SET.NAME("Term3",Term3+ni*(n-ni)*(n-ni))
=NEXT()
=SET.NAME("Term2",-2*dyx*Term2)
=SET.NAME("Term3",dyx*dyx*Term3)
=SET.NAME("SE1",SQRT(Term1+Term2+Term3)/Qcdt)
=SET.NAME("SE0",SQRT(Term1-(Qc-Qd)*(Qc-Qd)/n)/Qcdt)

=SELECT(OFFSET(RyCx,0,3,2,2))
=FORMAT.NUMBER("0.000")
=FORMULA("PK",OFFSET(RyCx,-1,3,1,1))
=FORMULA(PK,OFFSET(RyCx,0,3,1,1))
=FORMULA("SE",OFFSET(RyCx,-1,4,1,1))
=FORMULA(SE1,OFFSET(RyCx,0,4,1,1))
=FORMULA("SE1",OFFSET(RyCx,0,5,1,1))
=FORMULA(SE0,OFFSET(RyCx,1,4,1,1))
=FORMULA("SE0",OFFSET(RyCx,1,5,1,1))

=IF(JackOK=0)
=  BEEP()
=  FORMULA("Can't do jackknife; for two-level y,",OFFSET(xy,2,7,1,1))
=  FORMULA("need at least two cases for each level.",OFFSET(xy,3,7,1,1))
=ELSE.IF(JackOK=1)
=  FORMULA("PKm",OFFSET(RyCx,-1,2,1,1))
=  SELECT(OFFSET(RyCx,0,2,Ncases,1))
=  FORMAT.NUMBER("0.000")
=  SET.NAME("SPKm",0)
=  SET.NAME("SSPKm",0)

=  FOR("k",1,Ncases,1)
=    SET.NAME("Ry",INDEX(RyCx,k,1))
=    SET.NAME("Cx",INDEX(RyCx,k,2))
=    SET.NAME("Crc",INDEX(Cij,Ry,Cx))
=    SET.NAME("Drc",INDEX(Dij,Ry,Cx))
=    SET.NAME("Trc",INDEX(Tij,Ry,Cx))
=    SET.NAME("Qcm",Qc-2*Crc)
=    SET.NAME("Qdm",Qd-2*Drc)
=    SET.NAME("Qtxm",Qtx-2*Trc)
=    SET.NAME("Qcdtm",Qcm+Qdm+Qtxm)
=    SET.NAME("PKm",(Qcm+Qtxm/2)/Qcdtm)
=    FORMULA(PKm,OFFSET(RyCx,k-1,2,1,1))
=    SET.NAME("SPKm",SPKm+PKm)
=    SET.NAME("SSPKm",SSPKm+PKm*PKm)
=  NEXT()

=  SET.NAME("PKj",Ncases*PK-(Ncases-1)*SPKm/Ncases)
=  SET.NAME("SEj",SQRT((Ncases-1)*(SSPKm-SPKm*SPKm/Ncases)/Ncases))
=  SELECT(OFFSET(RyCx,3,3,1,2))
=  FORMAT.NUMBER("0.000")
=  FORMULA(PKj,OFFSET(RyCx,3,3,1,1))
=  FORMULA(SEj,OFFSET(RyCx,3,4,1,1))
=  FORMULA("Jack",OFFSET(RyCx,3,5,1,1))
=END.IF()

=SELECT(OFFSET(RyCx,0,3,5,3))
=FORMULA("DONE!",OFFSET(RyCx,4,4,1,1))
=BEEP()
=BEEP()
=HALT()
